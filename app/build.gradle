apply plugin: 'com.android.application'

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {

    viewBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion rootProject.ext.compileVersion
    defaultConfig {
        applicationId "com.ray.project"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.appVersionCode
        versionName rootProject.ext.appVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        signingConfig signingConfigs.debug

        ndk {
            // 设置支持的SO库架构 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
//            abiFilters 'armeabi'
        }

        // 默认是android的渠道
        manifestPlaceholders.put("RAY_CHANNEL_VALUE", "android")
        manifestPlaceholders.put("VERSION_CODE", rootProject.ext.appVersionCode)
        manifestPlaceholders.put("BUGLY_APPID", "188c656545")
        manifestPlaceholders.put("BUGLY_APPKEY", "b208da69-93d8-45c6-b5ee-0d48ae0dfd32")

        buildConfigField "String", "SHIPLY_APPID", "\"c6097cfbf6\""
        buildConfigField "String", "SHIPLY_APPKEY", "\"0cbb7413-6a79-4925-af9d-84ecc2e57ea8\""
        manifestPlaceholders.put("SHIPLY_APPID", "c6097cfbf6")
        manifestPlaceholders.put("SHIPLY_APPKEY", "0cbb7413-6a79-4925-af9d-84ecc2e57ea8")
    }

    signingConfigs {
        debug {
            storeFile file("../raykey")
            keyAlias "raykey"
            keyPassword "123456"
            storePassword "123456"
        }
        release {
            storeFile file("../raykey")
            keyAlias "raykey"
            keyPassword "123456"
            storePassword "123456"
        }
    }

    // 自定义打包时apk名字
    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            // 输出apk名称为app_v3.0(45)_pp.apk
            //outputFileName = "app_v${defaultConfig.versionName}(${defaultConfig.versionCode})_${variant.productFlavors[0].name}.apk"

            if(variant.buildType.name == ('release')) {
                outputFileName = "${variant.productFlavors[0].name}_app${defaultConfig.versionName}_${defaultConfig.versionCode}_${releaseTime()}.apk"
            } else if(variant.buildType.name == ('debug')) {
                outputFileName = "${variant.productFlavors[0].name}_app${defaultConfig.versionName}_${defaultConfig.versionCode}_debug.apk"
            }
        }
    }

    // 多渠道打包
    flavorDimensions "default"
    productFlavors {
        ray {
            dimension "default"
            // 修改包名
            // applicationId "com.xxx"
            manifestPlaceholders = [RAY_CHANNEL_VALUE: "android"]
            signingConfig signingConfigs.release
        }
        rayTest {
            dimension "default"
            // 修改包名
            // applicationId "com.xxx"
            manifestPlaceholders = [RAY_CHANNEL_VALUE: "testRay"]
            signingConfig signingConfigs.release
        }
    }
    productFlavors.configureEach {
        flavor -> flavor.manifestPlaceholders = [RAY_CHANNEL_VALUE: name]
    }

    buildTypes {
        debug {
            manifestPlaceholders =
                    [
                            LAUNCHER_MODE: "android.intent.category.LAUNCHER",
                            RELEASE_PACKAGE: "false"
                    ]
            buildConfigField "boolean", "releasePackage", "false"
        }

        release {
            manifestPlaceholders =
                    [
                            LAUNCHER_MODE: "android.intent.category.LAUNCHER",
                            RELEASE_PACKAGE: "true"
                    ]
            // true - 打开混淆
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "boolean", "releasePackage", "true"
        }
    }

    // 移除lint检测的error
    lintOptions {
        abortOnError false
    }

    //    splits {
    //        abi {
    //            enable true
    //            reset()
    //            include 'x86', 'armeabi-v7a','x86_64'
    //            universalApk true
    //        }
    //    }
    sourceSets {
        main {
            jniLibs.srcDir 'libs'
        }
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }
}

configurations.configureEach {
//    resolutionStrategy {
//        force "com.android.support:support-v4:28.0.0"
//    }
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            print(requested.name)
            details.useVersion rootProject.ext.supportVersion
//            if (!requested.name.startsWith("multidex")) {
//
//            }
        }
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
//    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportAppcompatVersion}"
    implementation "androidx.appcompat:appcompat:${rootProject.ext.supportAppcompatVersion}"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation "junit:junit:${rootProject.ext.junitVersion}"
    androidTestImplementation "com.android.support.test:runner:${rootProject.ext.testRunnerVerion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${rootProject.ext.testEspressoVersion}"
    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    // Retrofit通过GSON将结果转换为Bean对象
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    // 让Retrofit支持RxJava
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    // 日志拦截器
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    implementation 'com.github.bumptech.glide:glide:4.5.0'
    implementation 'jp.wasabeef:glide-transformations:4.3.0'
    // If you want to use the GPU Filters
    implementation 'jp.co.cyberagent.android:gpuimage:2.1.0'
    implementation 'top.zibin:Luban:1.1.8'

    implementation "com.tencent:mmkv:${rootProject.ext.mmkvVersion}"

    implementation project(':photoLibrary')

    implementation "androidx.room:room-runtime:${rootProject.ext.roomVersion}"
    // For Kotlin use kapt instead of annotationProcessor
    annotationProcessor "androidx.room:room-compiler:${rootProject.ext.roomVersion}"

    // 其中latest.release指代最新Bugly SDK版本号，也可以指定明确的版本号，例如4.0.0
    implementation "com.tencent.bugly:crashreport:${rootProject.ext.buglyVersion}"

    // shiply 全量更新、灰度更新、配置与开关发布
    implementation "com.tencent.shiply:upgrade:${rootProject.ext.shiplyVersion}"

    // osmdroid地图组件
    implementation "org.osmdroid:osmdroid-android:${rootProject.ext.osmdroidVersion}"
    // Adds basic support for WMS servers, specifically GeoServer
    implementation "org.osmdroid:osmdroid-wms:${rootProject.ext.osmdroidVersion}"

//    implementation 'com.github.bingoogolapple.BGAQRCode-Android:zxing:1.3.8'
    implementation project(':zxing')
    implementation project(':zbar')
}

// 接入RFix补丁组件
apply from: rootProject.file('shiply/gradle/android-rfix-integration.gradle')
apply from: rootProject.file('shiply/gradle/android-auto-backup.gradle')